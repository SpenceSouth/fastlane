String lookupProperty(Project project, String gradlePropName, String envVarName) {
    return project.hasProperty(gradlePropName) ? project.property(gradlePropName) : System.getenv()[envVarName]
}

Object[] versionInfo = screengrabVersion(rootProject.file('version.properties'))
String versionNameString = versionInfo[0] as String
int versionCodeInt = versionInfo[1] as int

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.3.4'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"

    defaultConfig {
        versionCode versionCodeInt
        versionName versionNameString
        minSdkVersion 8
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

publish {
    userOrg = 'fastlane'
    repoName = 'fastlane'
    groupId = 'tools.fastlane'
    artifactId = 'screengrab'
    publishVersion = versionNameString
    website = 'https://docs.fastlane.tools/actions/screengrab/'
    bintrayUser = lookupProperty(project, 'bintrayUser', 'BINTRAY_USER')
    bintrayKey = lookupProperty(project, 'bintrayKey', 'BINTRAY_KEY')
}

dependencies {
    implementation "androidx.test:support-annotations:1.1.0-alpha4"
    implementation 'androidx.test:runner:1.1.0-alpha4'
    implementation 'androidx.test:rules:1.1.0-alpha4'
    implementation 'androidx.test.espresso:espresso-core:3.1.0-alpha4'
    implementation 'com.androidx.test.uiautomator:uiautomator-v18:2.1.2'
    implementation 'com.jraska:falcon:1.0.4'
}
